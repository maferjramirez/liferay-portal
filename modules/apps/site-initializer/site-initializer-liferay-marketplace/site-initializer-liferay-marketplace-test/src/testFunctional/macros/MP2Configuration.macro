definition {

	static var companyWebId = "";
	static var clientExtension = "";

	macro addUserAndSetAccount {
		if (!(isSet(groupName))) {
			var groupName = "Marketplace Site";
		}

		JSONUser.addUser(
			userEmailAddress = ${userEmailAddress},
			userFirstName = ${userFirstName},
			userLastName = ${userLastName},
			userScreenName = ${userScreenName});

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = ${userEmailAddress});

		JSONRole.assignRoleToUser(
			roleTitle = ${regularRole},
			userEmailAddress = StringUtil.toLowerCase(${userEmailAddress}));

		JSONUser.addUserToSite(
			groupName = ${groupName},
			userEmailAddress = ${userEmailAddress});

		JSONRole.assignSiteRoleToUser(
			groupName = ${groupName},
			roleTitle = ${roleTitle},
			userEmailAddress = ${userEmailAddress});

		JSONAccountEntryUser.addExistUsertoAccount(
			accountEntryName = ${accountEntryName},
			userEmailAddress = ${userEmailAddress});

		JSONAccountRole.associateUser(
			accountEntryName = ${accountEntryName},
			accountRole = ${accountRole},
			userEmailAddress = ${userEmailAddress});
	}

	macro commerceTearDown {
		JSONUser.tearDownNonAdminUsers();

		CommerceJSONAccountsAPI._deleteAllCommerceAccounts();

		CommerceJSONOptionsAPI._deleteAllCommerceOptions();

		CommerceJSONSpecificationGroupsAPI._deleteAllCommerceSpecificationGroups();

		CommerceJSONSpecificationsAPI._deleteAllCommerceSpecifications();

		CommerceJSONChannelsAPI._deleteAllCommerceChannels();

		CommerceJSONCatalogsAPI._deleteAllNoSystemCommerceCatalogs();

		CommerceConfiguration.commerceInitializersTearDown();
	}

	macro marketplaceInitializersTearDown {
		var companyWebId = JSONCompany.getWebId();

		if (isSet(clientExtension)) {
			var clientExtension = "liferay-marketplace-custom-element";

			AntCommands.runCommand("build-test-workspace.xml", "undeploy-workspace-client-extension -Dvirtual.instance.id=${companyWebId} -Dworkspace.client.extension.name=${clientExtension}");
		}

		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.openURL();

			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			Site.openSitesAdmin();

			if (IsElementPresent(key_tableEntry = "Marketplace Site", locator1 = "LexiconTable#TABLE_ENTRY")) {
				Site.openSitesAdmin();

				PortletEntry.selectAll();

				Click(locator1 = "Icon#SITES_VERTICAL_ELLIPSIS");

				MenuItem.clickNoError(menuItem = "Delete");

				Click(locator1 = "Button#MODAL_DELETE");

				Alert.viewSuccessMessage();
			}

			Account.openAccountsAdmin();

			if (IsElementPresent(key_tableEntry = "QA", locator1 = "LexiconTable#TABLE_ENTRY")) {
				Account.tearDownCP();
			}

			MP2Configuration.commerceTearDown();

			if (isSet(doLogout)) {
				User.logoutPG();
			}
		}
	}

	macro marketplaceSetUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		var companyWebId = JSONCompany.getWebId();
		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var moduleBaseDir = "workspaces/liferay-marketplace-workspace";
		var projectDir = PropsUtil.get("project.dir");

		FileUtil.replaceStringInFile("${projectDir}/${moduleBaseDir}/gradle.properties", "liferay.workspace.home.dir=", "liferay.workspace.home.dir=${liferayHome}");

		var clientExtension = "liferay-marketplace-custom-element";

		AntCommands.runCommand("build-test-workspace.xml", "deploy-workspace-client-extension -Dvirtual.instance.id=${companyWebId} -Dworkspace.client.extension.name=${clientExtension}");

		WaitForConsoleTextPresent(value1 = "STARTED liferaymarketplacecustomelement");

		HeadlessSite.addSite(
			siteName = "Marketplace Site",
			templateName = "Liferay Marketplace",
			templateType = "Site Initializer");

		ApplicationsMenu.gotoSite(site = "Marketplace Site");
	}

}