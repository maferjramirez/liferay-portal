import com.liferay.gradle.plugins.defaults.task.ReplaceRegexTask

apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)
task updateReadme(type: ReplaceRegexTask)

String gradleVersion = "6.9.2"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from configurations.compileClasspath
	into jar.destinationDir
}

dependencies {
	api group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.47"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
	compileOnly group: "com.liferay", name: "com.liferay.source.formatter", version: "1.0.1330"
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

pluginBundle {
	tags = ["java", "style"]
}

updateReadme {
	match(/group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.source\.formatter", version: "(\d.+)"/, "README.markdown")

	onlyIf {
		!project.version.endsWith("-SNAPSHOT")
	}

	replacement = {
		project.version
	}
}

publish {
	dependsOn updateReadme
}