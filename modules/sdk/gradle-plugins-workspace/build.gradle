import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.GradleUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"
apply plugin: "org.ysb33r.gradletest"

task copyBundle(type: Copy)
task copyDistBundleZipPluginsSdk(type: Copy)
task copyGradleTestDependencies(type: Copy)
task copyGradleTestProjects(type: Copy)
task copyHotFixZip(type: Copy)
task copyRepo(type: Copy)
task copyWorkspacesSamples
task downloadBundle(type: Download)
task downloadHotFixZip(type: Download)
task downloadPluginsSdk(type: Download)
task downloadProductInfo(type: Download)

String bundleUrl = "https://releases-cdn.liferay.com/portal/7.1.2-ga3/liferay-ce-portal-tomcat-7.1.2-ga3-20190107144105508.tar.gz"
File gradleTestDependenciesDir = new File(buildDir, "gradle-test-dependencies")
String gradleVersion = "7.3.3"
String hotFixZipUrl = "https://us-east-1.linodeobjects.com/devtools-s3.liferay.com/gradle-plugins-workspace/liferay-hotfix-1072-7210.zip"
String pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.22/com.liferay.portal.plugins.sdk-1.0.22-withdependencies.zip"

if (System.properties["http.proxyHost"] == "squid.lax.liferay.com") {
	bundleUrl = "http://mirrors.liferay.com/releases.liferay.com/portal/7.1.2-ga3/liferay-ce-portal-tomcat-7.1.2-ga3-20190107144105508.tar.gz"
	pluginsSdkUrl = "https://repository-cdn.liferay.com/nexus/service/local/repositories/liferay-public-releases/content/com/liferay/portal/com.liferay.portal.plugins.sdk/1.0.22/com.liferay.portal.plugins.sdk-1.0.22-withdependencies.zip"
}

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

cleanCopyLibs {
	enabled = false
}

copyBundle {
	dependsOn downloadBundle
	eachFile new StripPathSegmentsAction(1)

	from {
		tarTree(downloadBundle.dest)
	}

	includeEmptyDirs = false
	into new File(buildDir, "bundle")
}

copyDistBundleZipPluginsSdk {
	ext {
		autoClean = false
	}

	dependsOn downloadPluginsSdk
	eachFile new StripPathSegmentsAction(1)

	from {
		zipTree(downloadPluginsSdk.dest)
	}

	includeEmptyDirs = false
	into "src/gradleTest/distBundleZip/plugins-sdk"
}

copyGradleTestDependencies {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from configurations.compileClasspath
	from jar
	into gradleTestDependenciesDir

	doLast {
		configurations.compileClasspath.forEach {
			File compileClasspathFile ->

			if (compileClasspathFile.name.startsWith("com.liferay.portal.tools.soy.builder") || compileClasspathFile.name.startsWith("jackson-dataformat-yaml")) {
				copy {
					from compileClasspathFile
					into jar.destinationDir
				}
			}
		}
	}
}

copyGradleTestProjects {
	ext {
		autoClean = false
	}

	from("src/gradleTest/flexibleLayoutWildcard") {
		include "modules/**"
		include "themes/**"
		include "wars/**"
	}

	into "src/gradleTest/excludePatterns"
}

copyHotFixZip {
	ext {
		autoClean = false
	}

	dependsOn downloadHotFixZip

	from {
		downloadHotFixZip.dest
	}

	into "src/gradleTest/startDockerContainerWithPatch/configs/common/patching"
}

copyRepo {
	dependsOn copyGradleTestDependencies

	from gradleTestDependenciesDir
	into new File(buildDir, "gradleTest/repo")
}

copyWorkspacesSamples {
	doLast {
		[
			"sample-custom-element-1",
			"sample-iframe-1",
			"sample-theme-css-1",
			"sample-theme-favicon",
		].each {
			String sampleName ->

			copy {
				from new File(GradleUtil.getRootDir(project.getRootProject(), "portal-impl"), "workspaces/liferay-sample-workspace/client-extensions/liferay-${sampleName}")
				into "src/gradleTest/clientExtensionSamples/${sampleName}"
			}
		}
	}
}

dependencies {
	api group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "5.3.0"
	api group: "com.bmuschko", name: "gradle-docker-plugin", version: "6.7.0"
	api group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "2.12.5"
	api group: "com.github.docker-java", name: "docker-java", version: "3.2.8"
	api group: 'com.google.code.gson', name: 'gson', version: '2.9.0'
	api group: "com.liferay", name: "com.liferay.ant.bnd", version: "3.2.10"
	api(group: "com.liferay", name: "com.liferay.gradle.plugins", version: "14.0.168") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	api(group: "com.liferay", name: "com.liferay.gradle.plugins.css.builder", version: "5.0.9") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	api(group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "4.0.1") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	api group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "5.2.48"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.target.platform", version: "3.0.14"
	api(group: "com.liferay", name: "com.liferay.gradle.plugins.theme.builder", version: "2.0.25") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	api group: "com.liferay", name: "com.liferay.petra.lang", version: "5.1.2"
	api group: "com.liferay", name: "com.liferay.petra.string", version: "5.2.0"
	api(group: "com.liferay", name: "com.liferay.portal.tools.bundle.support", version: "3.7.4") {
		exclude group: "biz.aQute.bnd", module: "biz.aQute.bnd"
	}
	api(group: "com.liferay", name: "com.liferay.portal.tools.soy.builder", version: "3.0.4") {
		exclude group: "com.google.template", module: "soy"
		exclude group: "org.codehaus.plexus"
		exclude group: "org.sonatype.plexus"
	}
	api group: "de.undercouch", name: "gradle-download-task", version: "5.4.0"
	api group: "net.java.dev.jna", name: "jna", version: "5.8.0"
	api group: "net.saliman", name: "gradle-properties-plugin", version: "1.4.6"
	api group: "org.ow2.asm", name: "asm", version: "5.1"

	compileInclude files("lib/com.liferay.workspace.bundle.url.codec-1.0.0.jar")

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

downloadBundle {
	dest new File(buildDir, "bundle.tar.gz")
	onlyIfModified true
	src bundleUrl
}

downloadHotFixZip {
	dest new File(buildDir, "liferay-hotfix-1072-7210.zip")
	onlyIfModified true
	src hotFixZipUrl
}

downloadPluginsSdk {
	dest new File(buildDir, "plugins-sdk.zip")
	onlyIfModified true
	src pluginsSdkUrl
}

downloadProductInfo {
	dest buildDir
	onlyIfNewer true
	src "https://releases-cdn.liferay.com/tools/workspace/.product_info.json"
}

eclipse.classpath.file {
	whenMerged {
		classpath ->

		classpath.entries.removeAll {
			entry -> (entry.kind == "src") && (entry.path == "build/gradleTest/src/groovy")
		}
	}
}

gradleTest {
	dependsOn copyBundle
	dependsOn copyDistBundleZipPluginsSdk
	dependsOn copyGradleTestDependencies
	dependsOn copyGradleTestProjects
	dependsOn copyHotFixZip
	dependsOn copyRepo
	dependsOn copyWorkspacesSamples
	dependsOn jar

	deprecationMessagesAreFailures = false

	gradleArguments "--project-prop", "app.server.parent.dir=" + FileUtil.getAbsolutePath(copyBundle.destinationDir)
	gradleArguments "--project-prop", "pluginClasspathDir=${uri(gradleTestDependenciesDir)}"

	versions gradleVersion
}

gradleTestGenerator {
	mustRunAfter copyDistBundleZipPluginsSdk
	mustRunAfter copyGradleTestProjects
	mustRunAfter copyHotFixZip
}

liferayOSGi {
	expandCompileInclude = true
}

processResources {
	from downloadProductInfo
}

updateFileVersions {
	match(/name: "com\.liferay\.gradle\.plugins\.workspace", version: "(\d.+)"/, "../../../modules/sdk/project-templates/project-templates-workspace/src/main/resources/archetype-resources/settings.gradle")
	match(/name: "com\.liferay\.gradle\.plugins\.workspace", version: "(\d.+)"/, "../../../workspaces/liferay-sample-workspace/settings.gradle")
	match(/name: "com\.liferay\.gradle\.plugins\.workspace", version: "(\d.+)"/, "../../../workspaces/refresh_liferay_sample_workspace.sh")
}