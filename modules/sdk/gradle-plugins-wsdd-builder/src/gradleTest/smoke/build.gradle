buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.service.builder", version: "latest.release"
	}

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

subprojects {
	apply plugin: "java-library"

	group = "com.example.sample"

	repositories {
		File dir = rootProject.projectDir

		while (dir != null) {
			File tmpMavenRepositoryDir = new File(dir, ".m2-tmp")

			if (tmpMavenRepositoryDir.exists()) {
				maven {
					url tmpMavenRepositoryDir
				}
			}

			dir = dir.getParentFile();
		}

		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

task runGradleTest

project(":sample-api") {
	dependencies {
		api group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
		api group: "com.liferay", name: "com.liferay.osgi.util", version: "3.0.0"
		api group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "4.81.2"
		api group: "org.osgi", name: "osgi.core", version: "6.0.0"
	}
}

project(":sample-service") {
	apply plugin: "com.liferay.portal.tools.service.builder"
	apply plugin: "com.liferay.portal.tools.wsdd.builder"

	buildService {
		apiDir = "../sample-api/src/main/java"
		osgiModule = true
	}

	dependencies {
		api group: "com.liferay", name: "com.liferay.petra.string", version: "5.0.1"
		api group: "com.liferay", name: "com.liferay.portal.spring.extender", version: "2.0.0"
		api project(":sample-api")

		serviceBuilder group: "com.liferay", name: "com.liferay.portal.tools.service.builder", version: "1.0.462"
	}
}

runGradleTest {
	Project serviceProject = project(":sample-service")

	dependsOn serviceProject.tasks.buildWSDD

	doLast {
		String[] fileNames = [
			"server-config.wsdd",
			"src/main/resources/com/example/sample/service/http/FooService_deploy.wsdd",
			"src/main/resources/com/example/sample/service/http/FooService_undeploy.wsdd"
		]

		for (String fileName in fileNames) {
			File generatedFile = serviceProject.file(fileName)

			assert generatedFile.exists()
		}
	}
}