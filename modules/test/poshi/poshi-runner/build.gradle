import com.liferay.gradle.plugins.defaults.task.ReplaceRegexTask
import com.liferay.gradle.util.copy.RenameDependencyClosure

apply plugin: "com.liferay.poshi.runner"

buildCSS {
	ext {
		autoClean = false
	}
}

dependencies {
	api group: "com.deque.html.axe-core", name: "selenium", version: "4.6.0"
	api group: "com.github.lewka", name: "ocular", version: "1.0.5"
	api group: "com.github.scribejava", name: "scribejava-core", version: "3.2.0"
	api group: "com.google.guava", name: "guava", version: "32.0.1-jre"
	api group: "com.jayway.jsonpath", name: "json-path", version: "2.1.0"
	api group: "com.liferay", name: "com.liferay.poshi.core", version: "1.0.139"
	api group: "com.liferay", name: "jodd.util", version: "6.0.1.LIFERAY-PATCHED-1"
	api group: "com.liferay", name: "net.jsourcerer.webdriver.JSErrorCollector", version: "0.6"
	api group: "com.sun.mail", name: "jakarta.mail", version: "1.6.6"
	api group: "commons-io", name: "commons-io", version: "2.11.0"
	api group: "commons-lang", name: "commons-lang", version: "2.6"
	api("io.netty:netty-transport") {
		version {
			strictly "4.1.84.Final"
		}
	}
	api group: "junit", name: "junit", version: "4.13.1"
	api group: "net.lightbody.bmp", name: "browsermob-core", version: "2.1.5"
	api group: "org.bytedeco.javacpp-presets", name: "opencv", version: "2.4.10-0.10"
	api group: "org.dom4j", name: "dom4j", transitive: false, version: "2.1.3"
	api group: "org.json", name: "json", version: "20230227"
	api group: "org.jsoup", name: "jsoup", version: "1.15.3"
	api group: "org.seleniumhq.selenium", name: "selenium-devtools-v100", version: "4.2.2"
	api group: "org.seleniumhq.selenium", name: "selenium-java", version: "4.7.2"
	api group: "org.sikuli", name: "sikuli-api", version: "1.2.0"
	api group: "xml-apis", name: "xml-apis", version: "1.4.01"

	compileOnly project(":test:data-guard-connector")
}

task release(type: ReplaceRegexTask)

deploy {
	from configurations.runtimeOnly
	rename new RenameDependencyClosure(project, configurations.runtimeOnly.name)
}

liferay {
	deployDir = project.properties["sdk.dir"] + "/dependencies/com.liferay.poshi.runner/lib"
}

liferayOSGi {
	expandCompileInclude = true
}

release {
	String releaseVersion = project.getVersion()

	if (project.hasProperty("releaseVersion")) {
		releaseVersion = getProperty("releaseVersion")
	}

	setReplacement(releaseVersion)

	match(/private Object _version = "(\d.+)";/, "../../../sdk/gradle-plugins-poshi-runner/src/main/java/com/liferay/gradle/plugins/poshi/runner/PoshiRunnerExtension.java")
}

test {
	systemProperty "webdriver.chrome.driver", "${buildDir}/webdriver/chromedriver"

	dependsOn downloadWebDriverBrowserBinary
}